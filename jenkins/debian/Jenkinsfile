def pipeline_version = VersionNumber(versionNumberString: '${BUILD_DATE_FORMATTED,"yyyyMMdd"}.${BUILDS_TODAY_Z}')
def kernelci_arches = ["armel"] //, "arm64"] //, "x86", "x86_64"]
def debian_arches = ["armhf", "arm64", "i386", "amd64"]
def toolchain_arches = ["arm-linux-gnueabihf", "aarch64-linux-gnu", "i386-linux-gnu", "x86_64-linux-gnu"]

def makeImageStep(String pipeline_version, String kernelci_arch, String debian_arch) {
  return {
    node("docker-slave") {
      stage("Checkout") {
        checkout scm
      }

      docker.build("debian", "-f jenkins/debian/Dockerfile_debos --pull .").inside("--device=/dev/kvm") {
        stage("Build base image for ${kernelci_arch}") {
          sh """
            mkdir -p ${pipeline_version}/${kernelci_arch}
            debos -t architecture:${debian_arch} -t extra_packages:'libpciaccess0 libkmod2 libprocps6 libcairo2 libunwind8 libudev1 libglib2.0-0' jenkins/debian/debos/stretch.yaml
          """
          stash includes: "rootfs.cpio.gz", name: "rootfs-${kernelci_arch}"
          archiveArtifacts artifacts: "rootfs.cpio.gz", fingerprint: true
        }
      }
    }
  }
}

def makeBuildStep(String pipeline_version, String kernelci_arch, String debian_arch, String toolchain_arch) {
  return {
    node("docker-slave") {
      stage("Checkout") {
        checkout scm
      }

      docker.build("debian", "-f jenkins/debian/Dockerfile_debian --pull --build-arg=\"DEBIAN_ARCH=${debian_arch}\" .").inside("--device=/dev/kvm") {
        dir("${pipeline_version}") {
          dir("v4l2-compliance") {
            stage("Build v4l2-compliance for ${kernelci_arch}") {
              git url: "git://linuxtv.org/v4l-utils.git"
              sh """
                sh bootstrap.sh
                mkdir install
                PKG_CONFIG_PATH=/usr/lib/${toolchain_arch}/pkgconfig/ ./configure --host=${toolchain_arch} --prefix=\$(pwd)/install --with-udevdir=\$(pwd)/install/lib/udev
                make -j\$(nproc) V=1
                make V=1 install
                find install
                mkdir -p usr/bin usr/lib usr/lib/libv4l
                cp install/bin/* install/sbin/* usr/bin/.
                cp -rf install/lib/*.so* usr/lib/.
                cp -rf install/lib/libv4l/*.so* usr/lib/libv4l/.
                ${toolchain_arch}-strip usr/bin/* usr/lib/*.so* usr/lib/libv4l/*.so*
                find -H usr | cpio -H newc -v -o | gzip -c - > v4l2-compliance.cpio.gz
              """
              stash includes: "v4l2-compliance.cpio.gz", name: "v4l2-compliance-${kernelci_arch}"
              archiveArtifacts artifacts: "v4l2-compliance.cpio.gz", fingerprint: true
            }
          }
        }
      }
    }
  }
}

def stepsForParallel = [:]
for (int i = 0; i < kernelci_arches.size(); i++) {
    def s = kernelci_arches.get(i)
    def buildStep = "Build image for ${s}"
    stepsForParallel[buildStep] = makeImageStep(pipeline_version,
                                                kernelci_arches.get(i),
                                                debian_arches.get(i))
    buildStep = "Build test suite for ${s}"
    stepsForParallel[buildStep] = makeBuildStep(pipeline_version,
                                                kernelci_arches.get(i),
                                                debian_arches.get(i),
                                                toolchain_arches.get(i))
}

parallel stepsForParallel

node {
  try {
    sh "rm -rf *"
    sh "ls -l"
    for (int i = 0; i < kernelci_arches.size(); i++) {
      def arch = kernelci_arches.get(i)
      stage("Adding test suite to image for ${arch}") {
        unstash "rootfs-${arch}"
        unstash "v4l2-compliance-${arch}"
        sh "gunzip rootfs.cpio.gz"
        dir("ramdisk") {
          sh """
            zcat ../v4l2-compliance.cpio.gz | cpio -iud
            find . | cpio --format=newc --verbose --create --append --file=../rootfs.cpio
          """
        }
        sh "gzip -c rootfs.cpio > rootfs-v4l2-${arch}.cpio.gz"
        archiveArtifacts artifacts: "rootfs-v4l2-${arch}.cpio.gz", fingerprint: true
      }
    }
  } finally {
    deleteDir()
  }
}
